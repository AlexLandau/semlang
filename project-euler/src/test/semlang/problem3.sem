//The prime factors of 13195 are 5, 7, 13 and 29.
//What is the largest prime factor of the number 600851475143 ?

function euler.getLargestPrimeFactor(n: Natural): Natural {
    let primeFactors: List<Natural> = getPrimeFactors(n)
    let largestPrimeFactor: Natural = Try.assume<Natural>(Natural.max(primeFactors))
    largestPrimeFactor
}

function getPrimeFactors(n: Natural): List<Natural> {
    let primes: Sequence<Natural> = getPrimes()
    let firstPrimeDivisor: Natural = primes->first(dividesEvenly|(_, n))
    if (Natural.equals(n, firstPrimeDivisor)) {
        List.singleton<Natural>(n)
    } else {
        let dividend: Natural = Try.assume<Natural>(Natural.divide(n, firstPrimeDivisor))
        let recursiveFactors: List<Natural> = getPrimeFactors(dividend)
        List.append<Natural>(recursiveFactors, firstPrimeDivisor)
    }
}

// Library candidate for a non-standard library?
function getPrimes(): Sequence<Natural> {
    let 2: Natural = Natural."2"
    Sequence.createAccumulating<Natural>(2, getNextPrime|(_))
}

function getNextPrime(allPreviousPrimes: List<Natural>): Natural {
    let lastPrime: Natural = Try.assume<Natural>(List.last<Natural>(allPreviousPrimes))
    let firstCandidate: Natural = Natural.plus(lastPrime, Natural."1")
    let candidates: Sequence<Natural> = Natural.arithmeticProgression(firstCandidate, Natural."1")
    candidates->first(isNotDivisibleByAny|(_, allPreviousPrimes))
}

function isNotDivisibleByAny(n: Natural, allPreviousPrimes: List<Natural>): Boolean {
    let divisibilities: List<Boolean> = List.map<Natural, Boolean>(allPreviousPrimes, dividesEvenly|(_, n))
    Boolean.not(Boolean.any(divisibilities))
}

function dividesEvenly(denominator: Natural, numerator: Natural): Boolean {
    let remainder: Natural = Natural.remainder(numerator, denominator)
    Natural.equals(remainder, Natural."0")
}
