
// Represents a non-negative hex value
@Export
struct HexString {
  string: Unicode.String
  requires {
    Boolean.and(
      Boolean.not(List.isEmpty(string->codePoints)),
      Boolean.all(
        List.map(
          string->codePoints,
          isValidHexCodePoint|(_)
        )
      )
    )
  }
}

function getHexCodePointValue(codePoint: Unicode.CodePoint): Maybe<Natural> {
  if (Natural.isBetweenInclusive(codePoint->natural, Natural."48", Natural."57")) {
    // Note: In these cases, absoluteDifference() should act as minus().
    // Not sure if this is the right permanent solution.
    Maybe.success(Natural.absoluteDifference(codePoint->natural, Natural."48"))
  } else {
    if (Natural.isBetweenInclusive(codePoint->natural, Natural."97", Natural."102")) {
      Maybe.success(Natural.absoluteDifference(codePoint->natural, Natural."87"))
    } else {
      Maybe.failure<Natural>()
    }
  }
}

function isValidHexCodePoint(codePoint: Unicode.CodePoint): Boolean {
  Maybe.isSuccess(getHexCodePointValue(codePoint))
//  Boolean.or(
//    // Number case: 0-9
//    Natural.isBetweenInclusive(codePoint->natural, Natural."48", Natural."57"),
//    // Letter case: a-f (lowercase only)
//    Natural.isBetweenInclusive(codePoint->natural, Natural."97", Natural."102")
//  )
}

@Export
function HexString.toValue(hexString: HexString): Natural {
  List.reduce(
    hexString->string->codePoints,
    Natural."0",
    hexStringReduction|(_, _)
  )
}

function hexStringReduction(numberSoFar: Natural, nextCodePoint: Unicode.CodePoint): Natural {
  let prefixValue = Natural.times(numberSoFar, Natural."16")
  let currentDigitValue = Maybe.assume(getHexCodePointValue(nextCodePoint))
  Natural.plus(prefixValue, currentDigitValue)
}

