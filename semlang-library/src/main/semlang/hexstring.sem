
// Represents a non-negative hex value
@Exported
struct HexString {
  value: Unicode.String
  requires {
    Boolean.and(
      Boolean.not(List.isEmpty<Unicode.CodePoint>(value->value)),
      Boolean.all(
        List.map<Unicode.CodePoint, Boolean>(
          value->value,
          isValidHexCodePoint|(_)
        )
      )
    )
  }
}

function getHexCodePointValue(codePoint: Unicode.CodePoint): Try<Natural> {
  if (Natural.isBetweenInclusive(codePoint->value, Natural."48", Natural."57")) {
    // Note: In these cases, absoluteDifference() should act as minus().
    // Not sure if this is the right permanent solution.
    Try.success<Natural>(Natural.absoluteDifference(codePoint->value, Natural."48"))
  } else {
    if (Natural.isBetweenInclusive(codePoint->value, Natural."97", Natural."102")) {
      Try.success<Natural>(Natural.absoluteDifference(codePoint->value, Natural."87"))
    } else {
      Try.failure<Natural>()
    }
  }
}

function isValidHexCodePoint(codePoint: Unicode.CodePoint): Boolean {
  Try.isSuccess<Natural>(getHexCodePointValue(codePoint))
//  Boolean.or(
//    // Number case: 0-9
//    Natural.isBetweenInclusive(codePoint->value, Natural."48", Natural."57"),
//    // Letter case: a-f (lowercase only)
//    Natural.isBetweenInclusive(codePoint->value, Natural."97", Natural."102")
//  )
}

@Exported
function HexString.toValue(hexString: HexString): Natural {
  List.reduce<Unicode.CodePoint, Natural>(
    hexString->value->value,
    Natural."0",
    hexStringReduction|(_, _)
  )
}

function hexStringReduction(numberSoFar: Natural, nextCodePoint: Unicode.CodePoint): Natural {
  let prefixValue = Natural.times(numberSoFar, Natural."16")
  let currentDigitValue = Try.assume<Natural>(getHexCodePointValue(nextCodePoint))
  Natural.plus(prefixValue, currentDigitValue)
}

