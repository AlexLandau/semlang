buildscript {
    ext.kotlin_version = '1.2.60'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

// Configure the Kotlin-based subprojects
project(":kotlin").subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: 'jacoco'
    jacoco {
        toolVersion = "0.8.2"
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        // Kotlin library
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testCompile 'junit:junit:4.12'
    }
}

// Make CircleCI's caching of dependencies actually work
task downloadDependencies {
    doLast {
        subprojects {
//            println("I'm in project ${project}")
            for (configuration in configurations) {
                // Certain dependencies can't be touched directly, so we need to whitelist the good ones
                if (['antlr', 'compile', 'testCompile'].contains(configuration.name)) {
//                    println("Configuration is ${configuration}")
//                    println "Dependencies are: ${configuration.files}"
                    // Force iteration over all the files to ensure the dependencies get downloaded
                    configuration.files.iterator().collect()
                }
            }
        }
    }
}

apply plugin: 'jacoco'
jacoco {
    toolVersion = "0.8.2"
}
task jacocoRootReport(type: JacocoReport) {
    def kotlinProjects = project(":kotlin").subprojects
    dependsOn(kotlinProjects.test)

    additionalSourceDirs = files(kotlinProjects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(kotlinProjects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(kotlinProjects.sourceSets.main.output)
    executionData = files(kotlinProjects.jacocoTestReport.executionData)

    reports {
        html.enabled = true
        xml.enabled = true
    }

    doFirst {
        executionData = files(executionData.findAll { it.exists() })
    }
}
